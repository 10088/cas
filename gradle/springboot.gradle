apply plugin: "org.springframework.boot"

import org.apache.tools.ant.taskdefs.condition.Os

/**
 * Make resources become reloadable.
 * Applicationâ€™s static resources are loaded from their source location.
 */
def webappResources = new File(project(":webapp").getProjectDir(), "resources")
def externalizedTemplates = new File("/etc/cas/templates")

sourceSets {
    bootRun {
        resources {
            srcDir(webappResources)
            srcDir(externalizedTemplates)
        }
    }
}

springBoot {
    mainClassName = project.ext.mainClassName

}

if (plugins.hasPlugin("war")) {
    javadoc {
        enabled false
    }
    war {
        enabled = !rootProject.publishingRelease
    }
    bootWar {
        mainClassName = project.ext.mainClassName
        enabled = !rootProject.publishingRelease
        
        def currentTime = java.time.ZonedDateTime.now()

        manifest {
            attributes(
                    "Automatic-Module-Name": project.name,
                    "Implementation-Title": project.name,
                    "Implementation-Vendor": project.group,
                    "Created-By": project.group,
                    "Implementation-Date": currentTime,
                    "Specification-Version": rootProject.currentRevision,
                    "Implementation-Version": project.version)
        }
        from "${buildDir}/resources/main", { into "WEB-INF/classes" }
    }
} else {
    bootJar {
        mainClassName = project.ext.mainClassName
        enabled = !rootProject.publishingRelease
        from "${buildDir}/resources/main", { into "WEB-INF/classes" }
    }
}

bootRun {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        dependsOn pathingJar
        doFirst {
            classpath = files("$buildDir/classes/main", "$buildDir/resources/main", pathingJar.archivePath)
        }
    }
    
    sourceResources sourceSets.bootRun
    systemProperties = System.properties
    
    def list = ["--add-modules", "java.se.ee" ]
    if (rootProject.enableRemoteDebugging) {
        list.add("-Xrunjdwp:transport=dt_socket,address=5000,server=y,suspend=n")
    }

    jvmArgs = list
}
