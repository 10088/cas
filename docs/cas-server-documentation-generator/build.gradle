description = "Apereo CAS Documentation Site Generator"

apply plugin: "org.asciidoctor.convert"

dependencies {
    implementation project(":api:cas-server-core-api-configuration-model")
    implementation project(":core:cas-server-core-configuration-metadata-repository")
    implementation libraries.springbootconfigmetadata
}

javadoc {
    enabled false
}

install {
    enabled = false
}

uploadArchives {
    enabled = false
}

asciidoctor {
    sourceDir = file('src/docs/asciidoc')
    outputDir = file('build/docs')
    logDocuments = true
    attributes 'source-highlighter': 'prettify', icons : 'font'
}

task deletePropertiesDocumentation(type: Delete) {
    delete fileTree('src/docs/asciidoc') {
        include '**/*.adoc'
    }
}

task generatePropertiesDocumentation(type: JavaExec) {
    doFirst {
        logger.info "Generating asciidoctor .adoc files for CAS configuration properties based on the generated metadata"
    }
    main = "org.apereo.cas.configuration.metadata.ConfigurationMetadataAsciidoctorGenerator"
    classpath = sourceSets.main.runtimeClasspath
}

asciidoctor.dependsOn(deletePropertiesDocumentation, generatePropertiesDocumentation)
